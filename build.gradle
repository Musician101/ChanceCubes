apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.buildnumber = 0
if (System.getenv('BUILD_NUMBER') != null)
    project.buildnumber = System.getenv('BUILD_NUMBER')
else
    logger.lifecycle "No build number detected, setting to 0"

group "chanceCubes.CCubesCore"
version project.mod_version

def spigotVersion = "1.15.2-R0.1-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    // Needed for NMS dependencies, courtesy of Spigot's BuildTools
    mavenLocal()
}

dependencies {
    compile "org.spigotmc:spigot-api:${spigotVersion}"
    compile "org.spigotmc:spigot:${spigotVersion}"
}

processResources {
    filesMatching("**/*.yml", {
        expand 'version': project.version
    })
}

configurations {
    deployerJars
}


jar {
    manifest {
        attributes(["Specification-Title": "chance_cubes",
                    "Specification-Vendor": "turkey2349 & Musician101",
                    "Specification-Version": "${spigotVersion}",
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"turkey2349",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

compileJava {
    options.encoding = 'UTF-8'
    def mainName = 'CCubesCore.java'
    def main = "src/main/java/chanceCubes/${mainName}"
    def tempDir = 'build/tmp/sourcesCache'
    doFirst {
        copy {
            from main
            into tempDir
        }

        ant.replace(file: main, token: '@VERSION@', value: project.version)
    }
    doLast {
        copy {
            from "${tempDir}/${mainName}"
            into project.file(main).parent
        }
    }
}
